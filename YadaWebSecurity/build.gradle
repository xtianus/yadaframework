plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

group = 'net.yadaframework'
version = '0.7.0'

apply plugin: 'eclipse-wtp'

eclipse {
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
	mavenLocal()
}

dependencies {
	api 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE', // Version is different from main lib
		'org.springframework.security:spring-security-config:5.5.0',
		'org.apache.tomcat:tomcat-servlet-api:8.0.48'

    implementation	project(':YadaWeb'),
    	'org.springframework:spring-webmvc:5.3.18',
    	'org.springframework:spring-context-support:5.3.18',
		'org.springframework:spring-orm:5.3.18',
 		'org.thymeleaf:thymeleaf-spring5:3.0.15.RELEASE',
    	'org.springframework.security:spring-security-web:5.5.0',
		'org.hibernate:hibernate-entitymanager:5.5.0.Final',
		'org.hibernate:hibernate-validator:5.4.2.Final',
		'commons-fileupload:commons-fileupload:1.3.2',
		'com.fasterxml.jackson.core:jackson-databind:2.12.3',
		'ch.qos.logback:logback-classic:1.3.15',
		'org.springframework.social:spring-social-facebook:2.0.3.RELEASE',
		'org.apache.commons:commons-lang3:3.7',
		'com.google.api-client:google-api-client-jackson2:1.32.1'
		;
		

    // testImplementation 'junit:junit:4.12'
}

configurations.all {
	resolutionStrategy {
		force 'com.google.code.gson:gson:2.8.5' // force 2.8.5 because 2.8.6 uses java 9
	}
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWebSecurity",
                   "Implementation-Version": archiveVersion)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadawebsecurity.properties", token: '@YADA_VERSION@', value: version)
	}
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

javadoc {
    options.encoding = 'UTF-8'
    options.docEncoding = 'UTF-8'
    options.charSet = 'UTF-8'
    failOnError = false
}

// http://stackoverflow.com/a/27853822/587641
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
} 

// PublishToMavenLocal task
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'yadawebsecurity'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionOf('runtimeClasspath')
                }
            }
  			pom {
			    name = 'YadaWebSecurity'
			    description = 'Yada Framework authentication and authorization component'
			    url = 'https://yadaframework.net/en/index.html'
			    inceptionYear = '2014'
			    packaging = 'jar'
			    licenses {
			        license {
			            name = 'MIT License'
			            url = 'https://en.wikipedia.org/wiki/MIT_License'
			        }
			    }
			    developers {
			        developer {
			            id = 'xtianus'
			            name = 'Studio Ghezzi'
			            email = 'studio@ghezzi.net'
			        }
			    }
			    organization {
			    	name = 'Studio Ghezzi'
			    	url = 'https://studio.ghezzi.net/'
			    }
			    scm {
			        connection = 'scm:git:git@github.com:xtianus/yadaframework.git'
			        developerConnection = 'scm:git:git@github.com:xtianus/yadaframework.git'
			        url = 'https://github.com/xtianus/yadaframework'
			    }
			}
        }
    }
    repositories {
        maven {
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials) {
            	username = ossrhUsername
            	password = ossrhPassword
            }
            // url = "file://${buildDir}/repotest"
        }
    }    
}

signing {
    sign publishing.publications.mavenJava
}

