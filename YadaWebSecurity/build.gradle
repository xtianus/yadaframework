plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'net.yadaframework'
version = '0.5.0'

apply plugin: 'eclipse-wtp'

eclipse {
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
	mavenLocal()
}

dependencies {
	api 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE',
		'org.springframework.security:spring-security-config:5.7.3',
		'org.apache.tomcat:tomcat-servlet-api:8.0.48'

    implementation	project(':YadaWeb'),
    	'org.springframework:spring-webmvc:5.3.23',
    	'org.springframework:spring-context-support:5.2.8.RELEASE',
 		'org.thymeleaf:thymeleaf-spring5:3.0.15.RELEASE',
    	'org.springframework.security:spring-security-web:5.7.3',
		// 'org.springframework.data:spring-data-jpa:1.11.9.RELEASE',
		'org.springframework.data:spring-data-jpa:2.6.4',
		'org.hibernate:hibernate-entitymanager:5.6.8.Final',
		'org.hibernate:hibernate-validator:5.4.3.Final',
		'commons-fileupload:commons-fileupload:1.3.3',
		'com.fasterxml.jackson.core:jackson-annotations:2.13.2',
		'com.fasterxml.jackson.core:jackson-databind:2.13.2',
		'ch.qos.logback:logback-classic:1.2.11',
		'org.springframework.social:spring-social-facebook:2.0.3.RELEASE',
		'org.apache.commons:commons-lang3:3.7',
		'com.google.api-client:google-api-client:1.24.2'
		;
		
		
    // testImplementation 'junit:junit:4.12'
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWebSecurity",
                   "Implementation-Version": version)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadawebsecurity.properties", token: '@YADA_VERSION@', value: version)
	}
}

// PublishToMavenLocal task
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'yadawebsecurity'
            from components.java
        }
    }
}