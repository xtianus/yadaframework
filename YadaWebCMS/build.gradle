plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'net.yadaframework'
version = '0.0.1'

apply plugin: 'eclipse-wtp'

eclipse {
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
	mavenLocal()
}

dependencies {
		
	implementation project(':YadaWeb'), project(':YadaWebSecurity'), 
		'org.springframework:spring-webmvc:5.2.8.RELEASE',
		'org.hibernate:hibernate-entitymanager:5.4.12.Final',
		'org.springframework.data:spring-data-jpa:2.3.2.RELEASE',
		'com.fasterxml.jackson.core:jackson-annotations:2.10.+',
		'com.fasterxml.jackson.core:jackson-core:2.10.+',
		'com.fasterxml.jackson.core:jackson-databind:2.10.+',
		'org.apache.commons:commons-io:1.3.2'
		
		
	testImplementation 'junit:junit:4.12'
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWebCMS",
                   "Implementation-Version": archiveVersion)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadawebcms.properties", token: '@YADA_VERSION@', value: archiveVersion)
	}
}

// PublishToMavenLocal task
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'yadawebcms'
            from components.java
        }
    }
}
