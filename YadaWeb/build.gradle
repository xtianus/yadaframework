buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		// Needed for test schema creation
		classpath 'net.yadaframework:yadatools:+'
		// classpath 'kr.motd.gradle:sphinx-gradle-plugin:2.2.0'
	}                    
}

plugins {
	id 'java-library'
	id 'eclipse-wtp'
	id 'maven-publish'
}

group = 'net.yadaframework'
version = '0.7.4.3'

java {
	withSourcesJar()
    withJavadocJar() // https://stackoverflow.com/a/75710366/587641
}

if (!project.hasProperty('repoPath')) {
	ext.repoPath="repoPathMissing"
}

eclipse {
	jdt {
		// sourceCompatibility = 1.8
		// targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
	classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
	// Using "api" to keep things simple: no need to repeat the libraries in other projects for compilation

	api 'org.apache.commons:commons-configuration2:2.+'
	
	// Fixes commons-text:1.9 CVE-2022-42889 caused by commons-configuration2:2.8.0
	api 'org.apache.commons:commons-text:1.+'
	
    compileOnly 'org.apache.tomcat.embed:tomcat-embed-core:8.5.+'
	// For embedded Tomcat ONLY - add these to your project
    // runtimeOnly 'org.apache.tomcat:tomcat-el-api:8.5.+'
    // runtimeOnly 'org.apache.tomcat:tomcat-jasper-el:8.5.+'
    // runtimeOnly 'org.apache.tomcat:tomcat-jasper:8.5.+'
	
	// Needed by Commons Configuration
    api 'commons-jxpath:commons-jxpath:1.+'
	
    api 'org.springframework:spring-webmvc:5.3.+',
		'org.springframework:spring-context-support:5.3.+',
		'org.springframework:spring-orm:5.3.+',
		// 'org.springframework.social:spring-social-facebook:2.0.3.RELEASE',
        'org.hibernate:hibernate-entitymanager:5.6.15.Final',
		'org.hibernate:hibernate-validator:5.4.3.Final',
		'org.thymeleaf:thymeleaf-spring5:3.1.1.RELEASE',
		'mysql:mysql-connector-java:8.+',
		'commons-collections:commons-collections:3.+',
		'org.apache.tomcat:tomcat-servlet-api:8.5.+',
		'commons-fileupload:commons-fileupload:1.5',
		'org.apache.commons:commons-exec:1.3',
		'com.sun.mail:javax.mail:1.+',
		'com.google.guava:guava:31.1-jre',
		'org.jsoup:jsoup:1.+',
		'ch.qos.logback:logback-classic:1.3.+',
		'com.fasterxml.jackson.core:jackson-databind:2.+',
		'org.seleniumhq.selenium:selenium-java:4.+',
		'com.drewnoakes:metadata-extractor:2.+',
		'org.flywaydb:flyway-mysql:9.+',
		'commons-beanutils:commons-beanutils:1.9.+'
		
	 // Connection pool for embedded Tomcat and programmatic datasource
	api 'org.vibur:vibur-dbcp:25.+'
	runtimeOnly 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.+'
	
	// Needed to run in Tomcat
	// runtime 'commons-jxpath:commons-jxpath:1.3'

	testImplementation 'junit:junit:4.+',
	 	'org.springframework:spring-test:4.3.+'
	
	// testRuntimeOnly 'commons-beanutils:commons-beanutils:1.9.+',
	// 	'commons-jxpath:commons-jxpath:1.3'
		
}

task listJars {
	doLast {
		configurations.compile.each { File file -> println file.name }
	}
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWeb",
                   "Implementation-Version": archiveVersion)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadaweb.properties", token: '@YADA_VERSION@', value: version)
	}
}

javadoc {
    options.encoding = 'UTF-8'
    options.docEncoding = 'UTF-8'
    options.charSet = 'UTF-8'
    failOnError = false
}

// http://stackoverflow.com/a/27853822/587641
// task javadocJar(type: Jar) {
//     classifier = 'javadoc'
//     from javadoc
// }   
// 
// artifacts {
//     archives javadocJar
// }

configurations {
	hibtools {
		extendsFrom configurations.implementation
	}
}

dependencies {
	hibtools files("$buildDir/classes/java/main")
	hibtools files("$buildDir/classes/java/test") // Needed for yadaPersistenceUnit
	hibtools 'org.hibernate:hibernate-tools:5.2.10.Final'
}

task testDbSchema(dependsOn: [testClasses], type: net.yadaframework.tools.YadaCreateDbSchemaTask) {
	inputs.files configurations.hibtools
	outputfilename = "V1__yadatest.sql"
	doFirst {
		new File("$buildDir/classes/java/test/META-INF").mkdirs()
		copy {
			from "$buildDir/resources/test/META-INF/persistence.test.xml"
			into "$buildDir/classes/java/test/META-INF"
			rename 'persistence.test.xml', 'persistence.xml'
		} 
	}
	doLast { 
		delete "$buildDir/classes/java/test/META-INF/persistence.xml"
	}
}

publishing {
    publications {
        yadaWebLibrary(MavenPublication) {
        	artifactId = 'yadaweb'
            from components.java
            
			pom {
			    name = 'YadaWeb'
			    description = 'Some useful tasks for the Yada Framework'
			    url = 'https://yadaframework.net/en/index.html'
			    inceptionYear = '2014'
			    packaging = 'jar'
			    licenses {
			        license {
			            name = 'MIT License'
			            url = 'https://en.wikipedia.org/wiki/MIT_License'
			        }
			    }
			    developers {
			        developer {
			            id = 'xtianus'
			            name = 'Studio Ghezzi'
			            email = 'studio@ghezzi.net'
			        }
			    }
			    organization {
			    	name = 'Studio Ghezzi'
			    	url = 'https://studio.ghezzi.net/'
			    }
			    scm {
			        connection = 'scm:git:git@github.com:xtianus/yadaframework.git'
			        developerConnection = 'scm:git:git@github.com:xtianus/yadaframework.git'
			        url = 'https://github.com/xtianus/yadaframework'
			    }
			}
        }
    }
    repositories {
        maven {
        	name "PublicRepo"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials) {
            	username = hasProperty('ossrhUsername')?ossrhUsername:''
            	password = hasProperty('ossrhPassword')?ossrhPassword:''
            }
        }
        // Creates task publishYadaWebLibraryPublicationToLocalRepoRepository
        maven {
        	name "LocalRepo"
        	// repoPath must be passed when invoking gradle using -P
        	url = "file://${repoPath}"
    	}
    }    
}

