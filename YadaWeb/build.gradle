buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		// Needed for test schema creation
		classpath 'net.yadaframework:yadatools:0.6.7.1'
		// classpath 'kr.motd.gradle:sphinx-gradle-plugin:2.2.0'
	}                    
}

plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'net.yadaframework'
version = '0.5.0'

// PublishToMavenLocal task
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'yadaweb'
            from components.java
        }
    }
}

apply plugin: 'eclipse-wtp'
// apply plugin: 'kr.motd.sphinx'

eclipse {
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
	classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
	mavenLocal()
}

dependencies {
	api 'org.apache.commons:commons-configuration2:2.+'
	
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:8.5.100'
	// For embedded Tomcat ONLY - add these to your project
    // runtimeOnly 'org.apache.tomcat:tomcat-el-api:8.5.+'
    // runtimeOnly 'org.apache.tomcat:tomcat-jasper-el:8.5.+'
    // runtimeOnly 'org.apache.tomcat:tomcat-jasper:8.5.+'
	
	// Needed by Commons Configuration
    runtimeOnly 'commons-jxpath:commons-jxpath:1.4.0'
	
    implementation 'org.springframework:spring-webmvc:5.3.23',
		'org.springframework:spring-context-support:5.3.23',
		// 'org.springframework.data:spring-data-jpa:1.11.9.RELEASE',
		'org.springframework.data:spring-data-jpa:2.7.3',
		'org.springframework.social:spring-social-facebook:2.0.3.RELEASE',
       'org.hibernate:hibernate-entitymanager:5.6.8.Final',
		'org.hibernate:hibernate-validator:5.4.3.Final',
		'org.thymeleaf:thymeleaf-spring5:3.0.15.RELEASE',
		'mysql:mysql-connector-java:8.+',
		'commons-collections:commons-collections:3.2.2',
		'org.apache.tomcat:tomcat-servlet-api:8.5.100',
		'commons-fileupload:commons-fileupload:1.3.3',
		'org.apache.commons:commons-exec:1.3',
		'com.sun.mail:javax.mail:1.6.2',
		'com.google.guava:guava:26.0-jre',
		'org.jsoup:jsoup:1.9.+',
		'com.fasterxml.jackson.core:jackson-annotations:2.13.2',
		'com.fasterxml.jackson.core:jackson-core:2.13.2',
		'com.fasterxml.jackson.core:jackson-databind:2.13.2',
		'org.seleniumhq.selenium:selenium-java:4.1.2',
		// 'org.seleniumhq.selenium:selenium-chrome-driver:3.141.59',
		// 'org.seleniumhq.selenium:selenium-support:3.141.59',
		// 'org.seleniumhq.selenium:selenium-firefox-driver:3.8.1',
		'com.drewnoakes:metadata-extractor:2.16.0',
		// 'org.littleshoot:littleproxy:1.1.2',
		// 'org.quartz-scheduler:quartz:2.2.3',
		'org.flywaydb:flyway-core:5.0.5',
		'commons-beanutils:commons-beanutils:1.9.4'
		
	 // Connection pool for embedded Tomcat and programmatic datasource
	implementation 'org.vibur:vibur-dbcp:25.0'
	runtimeOnly 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'
	
	// Needed to run in Tomcat
	// runtime 'commons-jxpath:commons-jxpath:1.3'

	testImplementation 'junit:junit:4.12',
		'org.springframework:spring-test:5.3.19'
	
	testRuntimeOnly 'commons-beanutils:commons-beanutils:1.9.4',
		'commons-jxpath:commons-jxpath:1.3'
		
}

task listJars {
	doLast {
		configurations.compile.each { File file -> println file.name }
	}
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWeb",
                   "Implementation-Version": version)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadaweb.properties", token: '@YADA_VERSION@', value: version)
	}
}


javadoc {
    options.encoding = 'UTF-8'
    options.docEncoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

// http://stackoverflow.com/a/27853822/587641
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}   

artifacts {
    archives javadocJar
}

configurations {
	hibtools {
		extendsFrom configurations.implementation
	}
}

dependencies {
	hibtools files("$buildDir/classes/java/main")
	hibtools files("$buildDir/classes/java/test") // Needed for yadaPersistenceUnit
	hibtools 'org.hibernate:hibernate-tools:5.2.10.Final'
}

task testDbSchema(dependsOn: [testClasses], type: net.yadaframework.tools.YadaCreateDbSchemaTask) {
	inputs.files configurations.hibtools
	outputfilename = "V1__yadatest.sql"
	doFirst {
		new File("$buildDir/classes/java/test/META-INF").mkdirs()
		copy {
			from "$buildDir/resources/test/META-INF/persistence.test.xml"
			into "$buildDir/classes/java/test/META-INF"
			rename 'persistence.test.xml', 'persistence.xml'
		} 
	}
	doLast { 
		delete "$buildDir/classes/java/test/META-INF/persistence.xml"
	}
}


